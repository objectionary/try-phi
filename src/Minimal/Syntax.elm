module Minimal.Syntax exposing
    ( Term(..), Object, AttrName, AttrValue(..)
    , Substitution, substituteXi, substituteLocator
    , whnf
    , incLocators, incLocatorsFrom
    )

{-| This module defines the syntax for the minimal ðœ‘-calculus.


# Definition

@docs Term, Object, Attr, AttrValue


# Locator substitution

@docs Substitution, substituteXi, substituteLocator


# Evaluation

@docs whnf


# Helpers

@docs incLocators, incLocatorsFrom

-}

import Dict exposing (Dict)


{-| Attribute name-}
type alias AttrName =
    String


{-| Value of attribute inside an object -}
type AttrValue
    = Void
    | Attached Term


{-| Object is a dictionary of attribute names and their values -}
type alias Object =
    Dict AttrName AttrValue


{-| TODO -}
type alias Substitution =
    Dict Int Term


{-| A term of ðœ‘-calculus is in one of the following forms:

1.  Object term: âŸ¦ aâ‚ â†¦ âˆ…, â€¦, aâ‚– â†¦ âˆ…, bâ‚ â†¦ tâ‚, â€¦, bâ‚™ â†¦ tâ‚™ âŸ§
1.  Attribute access via dot: t.a
1.  Application: tâ‚(a â†¦ tâ‚‚)
1.  Parent object locator: Ïâ¿

-}
type Term
    = Object Object
    | Dot Term AttrName
    | App Term ( AttrName, Term )
    | Locator Int


{-| Replace all parent objects locators referencing the closest outermost term (i.e. Î¾ or Ïâ°) with a given term.

    >>> t = Dot (Object (Dict.fromList [("x", Attached (Locator 0)), ("y", Attached (Locator 1))])) "x"
    >>> ppTerm t
    "âŸ¦ x â†¦ Î¾, y â†¦ Ï âŸ§.x" : String
    >>> ppTerm (substituteXi t t)
    "âŸ¦ x â†¦ Î¾, y â†¦ âŸ¦ x â†¦ Î¾, y â†¦ Ï.Ï âŸ§.x âŸ§.x" : String

-}
substituteXi : Term -> Term -> Term
substituteXi =
    substituteLocator 0


{-| TODO
-}
substituteLocator : Int -> Term -> Term -> Term
substituteLocator k parent term =
    case term of
        Locator n ->
            if n < k then
                Locator n

            else if n == k then
                parent

            else
                Locator (n - 1)

        Dot t a ->
            Dot (substituteLocator k parent t) a

        App t ( a, u ) ->
            App (substituteLocator k parent t) ( a, substituteLocator k parent u )

        Object o ->
            Object
                (Dict.map
                    (\a t ->
                        case t of
                            Void ->
                                Void

                            Attached u ->
                                Attached (substituteLocator (k + 1) (incLocators parent) u)
                    )
                    o
                )


{-| TODO
-}
incLocators : Term -> Term
incLocators =
    incLocatorsFrom 0


{-| TODO
-}
incLocatorsFrom : Int -> Term -> Term
incLocatorsFrom k term =
    case term of
        Object o ->
            Object
                (Dict.map
                    (\a t ->
                        case t of
                            Void ->
                                Void

                            Attached u ->
                                Attached (incLocatorsFrom (k + 1) u)
                    )
                    o
                )

        Dot t a ->
            Dot (incLocatorsFrom k t) a

        App t ( a, u ) ->
            App (incLocatorsFrom k t) ( a, incLocatorsFrom k u )

        Locator n ->
            if n >= k then
                Locator (n + 1)

            else
                Locator n


{-| Compute a ðœ‘-term to weak head normal form (WHNF).

    > t = Dot (Object (Dict.fromList [("x", Attached (Dot (Object (Dict.fromList [("y", Attached (Locator 2))])) "y"))])) "x"
    Dot (Object (Dict.fromList [("x",Attached (Dot (Object (Dict.fromList [("y",Attached (Locator 2))])) "y"))])) "x"
        : Term
    > ppTerm t
    "âŸ¦ x â†¦ âŸ¦ y â†¦ Ï.Ï âŸ§.y âŸ§.x" : String
    > ppTerm (whnf t)
    "Î¾" : String

-}
whnf : Term -> Term
whnf term =
    case term of
        Locator n ->
            Locator n

        App t ( a, u ) ->
            case whnf t of
                Object o ->
                    case Dict.get a o of
                        Just Void ->
                            Object (Dict.insert a (Attached (incLocators u)) o)

                        Just _ ->
                            App (Object o) ( a, u )

                        Nothing ->
                            App (Object o) ( a, u )

                t2 ->
                    App t2 ( a, u )

        Dot t a ->
            case whnf t of
                Object o ->
                    case Dict.get a o of
                        Just (Attached u) ->
                            whnf (substituteXi (Object o) u)

                        Just Void ->
                            Dot (Object o) a

                        Nothing ->
                            if Dict.member "ðœ‘" o then
                                Dot (Dot (Object o) "ðœ‘") a

                            else
                                Dot (Object o) a

                t2 ->
                    Dot t2 a

        Object o ->
            Object o
